S -> SELECT_SENTENCE | INSERT_EXPRESSION
INSERT_EXPRESSION -> 'INSERT' INSERT_TYPE 'TABLE' TABLE_NAMES PARTITION_EXPRESSION DATA_SENTENCE
INSERT_TYPE -> 'INTO' | 'OVERWRITE'
PARTITION_EXPRESSION -> 'PARTITION' L_PAR PARTITION_REFERENCE R_PAR EXISTS_EXPRESSION
EXISTS_EXPRESSION -> 'IF' NEGATION 'EXISTS' |
PARTITION_REFERENCE -> COLUMN_REFERENCE | COLUMN_REFERENCE '=' COLUMN_REFERENCE | PARTITION_REFERENCE COMMA PARTITION_REFERENCE
DATA_SENTENCE -> SELECT_SENTENCE | VALUE_EXPRESSION
VALUE_EXPRESSION -> 'VALUES' VALUE_REFERENCE
VALUE_REFERENCE -> L_PAR VALUES R_PAR | VALUE_REFERENCE COMMA VALUE_REFERENCE
VALUES -> WORD | 'NULL' | VALUES COMMA VALUES
SELECT_SENTENCE -> 'SELECT' SELECT_COMPLEMENT SELECT_EXPRESSION UNION_EXPRESSION
SELECT_COMPLEMENT -> 'ALL' | 'DISTINCT' |
SELECT_EXPRESSION -> COLUMN_EXPRESSION COMMA SELECT_EXPRESSION | COLUMN_EXPRESSION FROM_EXPRESSION
COLUMN_EXPRESSION -> COLUMN_REFERENCE COLUMN_ALIAS
COLUMN_REFERENCE -> SELECT_COMPLEMENT COLUMN_NAMES | SELECT_COMPLEMENT TABLE_NAMES POINT COLUMN_NAMES | WORD | FUNCTION | CASE_WHEN
CASE_WHEN -> 'CASE' WHEN_CONDITION 'END'
WHEN_CONDITION -> 'WHEN' CONDITION_EXPRESSION 'THEN' COLUMN_REFERENCE WHEN_CONDITION ELSE_EXPRESSION |
ELSE_EXPRESSION -> 'ELSE' COLUMN_REFERENCE |
FROM_EXPRESSION -> 'FROM' TABLE_EXPRESSION WHERE_EXPRESSION CLUSTER_EXPRESSION DISTIBUTE_EXPRESSION WINDOW_EXPRESSION GROUP_EXPRESSION ORDERING_EXPRESSION
TABLE_EXPRESSION -> TABLE_REFERENCE | JOIN_TABLE | TABLE_EXPRESSION COMMA TABLE_EXPRESSION
JOIN_TABLE -> INNER_JOIN | LEFT_RIGHT_FULL_JOIN | SEMI_JOIN | CROSS_JOIN
INNER_JOIN -> TABLE_REFERENCE INNER_REFERENCE 'JOIN' TABLE_REFERENCE JOIN_CONDITION | TABLE_REFERENCE INNER_REFERENCE 'JOIN' TABLE_REFERENCE
LEFT_RIGHT_FULL_JOIN -> TABLE_REFERENCE LEFT_RIGHT_FULL OUTER_REFERENCE 'JOIN' TABLE_REFERENCE JOIN_CONDITION
LEFT_RIGHT_FULL -> 'LEFT' | 'RIGHT' | 'FULL'
SEMI_JOIN -> TABLE_REFERENCE 'LEFT' 'SEMI' 'JOIN' TABLE_REFERENCE JOIN_CONDITION
CROSS_JOIN -> TABLE_REFERENCE 'CROSS' 'JOIN' TABLE_REFERENCE JOIN_CONDITION | 'CROSS' 'JOIN' TABLE_REFERENCE
INNER_REFERENCE -> 'INNER' |
OUTER_REFERENCE -> 'OUTER' |
JOIN_CONDITION -> 'ON' CONDITION_EXPRESSION
TABLE_REFERENCE -> TABLE_NAMES | TABLE_NAMES TABLE_ALIAS | L_PAR SELECT_SENTENCE R_PAR TABLE_ALIAS
TABLE_ALIAS -> 'AS' TABLE_NAMES | TABLE_NAMES
WHERE_EXPRESSION -> 'WHERE' CONDITION_EXPRESSION |
WINDOW_EXPRESSION -> 'WINDOW' COLUMN_REFERENCE 'AS' L_PAR PARTITION_BY_EXPRESSION ORDERING_EXPRESSION OVER_EXPRESSION R_PAR | WINDOW_EXPRESSION WINDOW_EXPRESSION |
CONDITION_EXPRESSION -> CONDITION | CONDITION_EXPRESSION LOGICAL_OPERATOR CONDITION_EXPRESSION | L_PAR CONDITION_EXPRESSION LOGICAL_OPERATOR CONDITION_EXPRESSION R_PAR
CONDITION -> COLUMN_REFERENCE NEGATION COMPARATOR COLUMN_REFERENCE
NEGATION -> 'NOT' |
LOGICAL_OPERATOR -> 'AND' | 'OR'
COMPARATOR -> '=' | '>=' | '>' | '<=' | '<' | 'LIKE' | 'IN' | 'IS' NEGATION 'NULL'
GROUP_EXPRESSION -> 'GROUP' 'BY' COLUMN_LIST HAVING_EXPRESSION |
COLUMN_LIST -> COLUMN_REFERENCE | COLUMN_REFERENCE COMMA COLUMN_LIST
HAVING_EXPRESSION -> 'HAVING' CONDITION_EXPRESSION |
ORDERING_CRITERION -> 'ASC' | 'DESC' |
ORDERING_COLUMN_LIST -> COLUMN_REFERENCE ORDERING_CRITERION | COLUMN_REFERENCE ORDERING_CRITERION COMMA ORDERING_COLUMN_LIST
ORDERING_EXPRESSION -> ORDER_EXPRESSION | SORT_EXPRESSION |
ORDER_EXPRESSION -> 'ORDER' 'BY' ORDERING_COLUMN_LIST
CLUSTER_EXPRESSION -> 'CLUSTER' 'BY' COLUMN_LIST BUCKET_EXPRESSION |
DISTIBUTE_EXPRESSION -> 'DISTRIBUTE' 'BY' COLUMN_LIST SORT_EXPRESSION |
BUCKET_EXPRESSION -> 'INTO' WORD 'BUCKETS' |
SORT_EXPRESSION -> 'SORT' 'BY' ORDERING_COLUMN_LIST |
FUNCTION -> SIMPLE_FUNCTION | TWO_ARGUMEMTS_FUNCTION | THREE_ARGUMEMTS_FUNCTION | ANALYTICS_FUNCTIONS
ANALYTICS_FUNCTIONS -> EMPTY_FUNCTION L_PAR R_PAR OVER_FUNCTION | EMPTY_FUNCTION L_PAR R_PAR 'OVER' COLUMN_REFERENCE
EMPTY_FUNCTION -> 'RANK' | 'ROW_NUMBER' | 'DENSE_RANK' | 'CUME_DIST' | 'PERCENT_RANK' | 'NTILE'
SIMPLE_FUNCTION -> SIMPLE_FUNCTION_NAMES L_PAR COLUMN_REFERENCE R_PAR OVER_FUNCTION
SIMPLE_FUNCTION_NAMES -> 'SUM' | 'COUNT' | 'MIN' | 'ISNULL' | 'ISNOTNULL' | 'AVG' | 'MEAN' | 'LEAD'
TWO_ARGUMEMTS_FUNCTION -> TWO_ARGUMEMTS_FUNCTION_NAMES L_PAR COLUMN_REFERENCE COMMA COLUMN_REFERENCE R_PAR OVER_FUNCTION
TWO_ARGUMEMTS_FUNCTION_NAMES -> 'SUBSTR' | 'NVL' | 'COALESCE'
THREE_ARGUMEMTS_FUNCTION -> THREE_ARGUMEMTS_FUNCTION_NAMES L_PAR COLUMN_REFERENCE COMMA COLUMN_REFERENCE COMMA COLUMN_REFERENCE R_PAR OVER_FUNCTION
THREE_ARGUMEMTS_FUNCTION_NAMES -> 'REGEXP_REPLACE' | 'IF' | 'LAG'
PARTITION_BY_EXPRESSION -> 'PARTITION' 'BY' ORDERING_COLUMN_LIST
OVER_FUNCTION -> 'OVER' L_PAR PARTITION_BY_EXPRESSION ORDERING_EXPRESSION OVER_EXPRESSION R_PAR |
OVER_EXPRESSION -> ROWS_RANGE_REFERENCE 'BETWEEN' BOUND_REFERENCE 'PRECEDING' 'AND' PRECEDING_REFERENCE
OVER_EXPRESSION -> ROWS_RANGE_REFERENCE 'BETWEEN' 'CURRENT' 'ROW' 'AND' CURRENT_ROW_EXPRESSION
OVER_EXPRESSION -> ROWS_RANGE_REFERENCE 'BETWEEN' WORD 'FOLLOWING' 'AND' BOUND_REFERENCE 'FOLLOWING'
OVER_EXPRESSION ->
CURRENT_ROW_EXPRESSION -> 'CURRENT' 'ROW' | BOUND_REFERENCE | 'FOLLOWING'
PRECEDING_REFERENCE -> WORD 'PRECEDING' | 'CURRENT' 'ROW' | BOUND_REFERENCE | 'FOLLOWING'
BOUND_REFERENCE -> 'UNBOUNDED' | WORD |
ROWS_RANGE_REFERENCE -> 'ROWS' | 'RANGE'
COLUMN_ALIAS -> 'AS' COLUMN_NAMES | COLUMN_NAMES |
UNION_EXPRESSION -> 'UNION' SELECT_COMPLEMENT SELECT_SENTENCE |
COLUMN_NAMES -> '*' | 'null' | 'NULL' | 'a' | 'b' | 'c' | 'd' | 'e' | 'total' | 'suma' | 't2_a' | 'a_s' | 'p' | 'where_column' | 'case_when'
TABLE_NAMES -> 't1'| 't2' | 't3' | 't4' | 't5' | 't6' | 'alias_t1'  | 'sub_2' | 'tp' | 'tp_sub'
WORD -> '#WORD#'
COMMA -> ','
POINT -> '.'
L_PAR -> '('
R_PAR -> ')'